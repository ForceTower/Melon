/*
 * This file is part of the UNES Open Source Project.
 * UNES is licensed under the GNU GPLv3.
 *
 * Copyright (c) 2020. Jo√£o Paulo Sena <joaopaulo761@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    ext.kotlin_version = '1.5.21'
    ext {
        juice = "1.7.1"
        snowpiercer = "1.1.4"

        // Kotlin Extensions
        ktx = '1.6.0'

        // Android X
        app_compat = '1.3.1'
        google_material = '1.4.0'
        constraint_layout = '2.1.0'
        annotation = '1.2.0'
        exif_interface = "1.1.0"
        pallete = "1.0.0"
        flexbox = "1.1.0"
        browser = '1.3.0'
        preference = '1.1.1'
        fragment = '1.3.6'
        swipe_refresh = '1.1.0'

        // Architecture
        room = '2.3.0'
        lifecycle = '2.3.1'
        paging = '3.0.1'
        navigation = '2.3.5'
        work_manager = '2.5.0'

        // Dependency Injection
        dagger = '2.35.1'
        hilt_androidx = '1.0.0'

        // Firebase
        firebase_core = '17.5.1'
        firebase_auth = '19.4.0'
        firebase_messaging = '20.3.0'
        firebase_firestore = '21.7.1'
        firebase_storage = '19.2.0'
        firebase_remote_config = '19.2.0'
        firebase_functions = '19.1.0'
        in_app_messaging = "19.0.1"
        firebase_crashlytics = '17.2.2'
        firebase_ui_storage = "7.1.1"

        // Logs
        timber = "4.7.1"

        // Image
        glide = '4.12.0'
        lottie = '3.7.0'
        image_cropper = "2.8.0"

        // Google Services
        gp_core = '1.10.0'
        gp_core_ktx = '1.8.1'
        gp_auth = '19.2.0'
        gp_games = '21.0.0'
        gp_location = '18.0.0'
        gp_billing_client = '4.0.0'
        gp_admob = '19.8.0'

        // Networking
        okhttp = '4.9.0'
        retrofit = '2.9.0'
        cookie_jar = "v1.0.1"

        // Others
        gson = '2.8.6'
        jsoup = '1.13.1'
        chart_view = "v3.1.0"
        about_libraries = "8.5.0"
        easy_permissions = "0.3.0"
        card_slider = '0.3.1'
        date_time_picker = '4.2.3'
        floating_search = "2.1.1"
        rx_kotlin = "2.4.0"

        // Build Versions
        android_gradle_plugin = '7.0.0'
        google_play_services = "4.3.4"
        kotlinter = "3.4.4"
        play_publisher = "3.5.0-agp7.0"
        bundletool = "0.10.0"
        crashlytics = '2.5.2'
        hilt_dagger = '2.38.1'
        coreDesugar = '1.1.5'
    }

    repositories {
        google()
        maven { url "https://jitpack.io" }
        maven { url "https://maven.fabric.io/public" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        mavenLocal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${android_gradle_plugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:$google_play_services"
        classpath "com.github.triplet.gradle:play-publisher:$play_publisher"
        classpath "org.jmailen.gradle:kotlinter-gradle:$kotlinter"
        classpath "com.android.tools.build:bundletool:$bundletool"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$crashlytics"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_dagger"
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin:$about_libraries"
    }

}

plugins {
    id "org.sonarqube" version "3.3"
}

allprojects {
    repositories {
        google()
        maven { url "https://jitpack.io" }
        maven { url "https://maven.fabric.io/public" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        mavenLocal()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "10000"
            options.compilerArgs << "-Xmaxwarns" << "10000"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//task reallyImportantTask {
//    try {
//        def exec = new File(project.rootDir, "exec_first_config")
//        if (!exec.exists()) {
//            def stream = AudioSystem.getAudioInputStream(new File(project.rootDir, "gradle_usefull/bee.wav"))
//            Clip clip = AudioSystem.getClip()
//            clip.open(stream)
//            clip.start()
//            exec.createNewFile()
//        }
//    } catch (Throwable ignored) {}
//}

subprojects {
    tasks.whenTaskAdded {
        if (name == 'preBuild') {
            mustRunAfter 'lintKotlin'
        }
    }

    sonarqube {
        properties {
            property "sonar.sources", "src/main"
        }
    }
}

subprojects {
    if (!gradle.startParameter.taskNames.any { it.contains("lint") }) {
        tasks.configureEach {
            if (name.startsWith("lint")) {
                enabled = false
            }
        }
    } else {
        // https://github.com/JakeWharton/timber/issues/408
        it.afterEvaluate {
            if (it.plugins.hasPlugin("com.android.application") || it.plugins.hasPlugin("com.android.library") || it.plugins.hasPlugin("com.android.dynamic-feature")) {
                it.android.lintOptions.disable = ["LogNotTimber", "StringFormatInTimber", "ThrowableNotAtBeginning", "BinaryOperationInTimber", "TimberArgCount", "TimberArgTypes", "TimberTagLength", "TimberExceptionLogging"]
            }
        }
    }
}

apply from: "versioning.gradle"
