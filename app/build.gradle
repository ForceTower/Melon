apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'

configurations {
    ktlint
}

android {
    compileSdkVersion sdkVersions.compile
    defaultConfig {
        applicationId "com.forcetower.uefs"
        minSdkVersion sdkVersions.min
        targetSdkVersion sdkVersions.target
        multiDexEnabled true
        def (code, name) = buildVersion()
        versionCode code
        versionName name

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        copy {
            from "../secret/"
            into "."
            include "**/*.json"
        }
    }
    signingConfigs {
        release {
            storeFile signFile
            try {
                String contents = new File('../keystore_auth').text
                String[] values = contents.split(";")
                storePassword values[0]
                keyAlias values[1]
                keyPassword values[2]
            } catch (Throwable ignored) {
                try {
                    String contents = new File('keystore_auth').text
                    String[] values = contents.split(";")
                    storePassword values[0]
                    keyAlias values[1]
                    keyPassword values[2]
                } catch (Throwable ignored2) {
                    println ignored2.message
                    storePassword "123456"
                    keyAlias "alias"
                    keyPassword "123456"
                }
            }
        }
    }
    buildTypes {
        debug {
            //ext.enableCrashlytics = false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
    }
    dataBinding {
        enabled = true
    }
    kapt {
        correctErrorTypes = true
        javacOptions {
            option("-Xmaxerrs", 1000)
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    lintOptions {
        abortOnError true
    }
}

afterEvaluate {

}

play {
    serviceAccountCredentials = file('../google_play_console_auth.json')
    track = "internal"
    defaultToAppBundles = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //Kotlin Dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktx"
    ktlint 'com.github.shyiko:ktlint:0.29.0'

    //Android Component
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "com.google.android.material:material:$google_material"
    implementation "androidx.palette:palette:$pallete"
    implementation "androidx.browser:browser:$browser"
    implementation "androidx.preference:preference:$preference"
    implementation "com.google.android:flexbox:0.3.2"

    //Lifecycle Extensions
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    //Navigation
    implementation "android.arch.navigation:navigation-fragment:$nav_version"
    implementation "android.arch.navigation:navigation-ui:$nav_version"
    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"

    //Paging
    implementation "androidx.paging:paging-runtime:$paging_version"

    //Room Database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Work Manager
    implementation("android.arch.work:work-runtime:$work_version") {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    implementation("android.arch.work:work-runtime-ktx:$work_version") {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    implementation("android.arch.work:work-firebase:$work_version") {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    //In-app purchases!
    implementation 'com.android.billingclient:billing:1.2'

    //Dagger Dependency Injection
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    //Firebase
    implementation "com.google.firebase:firebase-core:$firebase_core"
    implementation "com.google.firebase:firebase-auth:$firebase_auth"
    implementation "com.google.firebase:firebase-messaging:$firebase_messaging"
    implementation "com.google.firebase:firebase-firestore:$firebase_firestore"
    implementation "com.google.firebase:firebase-storage:$firebase_storage"
    implementation 'com.firebaseui:firebase-ui-storage:4.2.0'

    //Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    //Google Play Services
    implementation 'com.google.android.gms:play-services-games:16.0.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.android.gms:play-services-location:16.0.0'

    //Internet / API Connection
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    //DOM Parser
    implementation 'org.jsoup:jsoup:1.11.3'
    //Cookie Handler
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    //Charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    //Debug Use
    implementation "com.jakewharton.timber:timber:$timber"

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'

    //Lottie Animations!
    implementation 'com.airbnb.android:lottie:2.8.0'

    //Open Source Licenses
    implementation "com.mikepenz:aboutlibraries:6.2.0"

    //Permissions Helper
    implementation 'pub.devrel:easypermissions:0.3.0'

    api 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    //Helper Projects
    implementation project(path: ':sagres')
    implementation project(path: ':bypass')
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

apply from: "../artifacts.gradle"
apply plugin: 'com.google.gms.google-services'
